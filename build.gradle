buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}


group = 'kotlinfx'
version = '0.1-SNAPSHOT'


allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}


apply plugin: "kotlin"

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    // And all that just to make org.eclipse.jdt.core work
    compile 'org.eclipse.core:org.eclipse.core.commands:3.6.0'
    compile 'org.eclipse.core:org.eclipse.core.contenttype:3.4.100'
    compile 'org.eclipse.core:org.eclipse.core.expressions:3.4.300'
    compile 'org.eclipse.core:org.eclipse.core.filesystem:1.3.100'
    compile 'org.eclipse.core:org.eclipse.core.jobs:3.5.100'
    compile 'org.eclipse.core:org.eclipse.core.resources:3.7.100'
    compile 'org.eclipse.core:org.eclipse.core.runtime:3.7.0'
    compile 'org.eclipse.equinox:org.eclipse.equinox.app:1.3.100'
    compile 'org.eclipse.equinox:org.eclipse.equinox.common:3.6.0'
    compile 'org.eclipse.equinox:org.eclipse.equinox.preferences:3.4.1'
    compile 'org.eclipse.equinox:org.eclipse.equinox.registry:3.5.101'
    compile 'org.eclipse.osgi:org.eclipse.osgi:3.7.1'
    compile 'org.eclipse.text:org.eclipse.text:3.5.101'
    compile 'com.reubenpeeris:org.eclipse.jdt.core:3.9.50.v20140317-1741' // has Java 8 support +1!
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}


subprojects {
    buildscript {
        repositories {
            mavenCentral()
            maven {
                url 'http://oss.sonatype.org/content/repositories/snapshots'
            }
        }
        dependencies {
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        }
    }

    apply plugin: 'kotlin'

    repositories {
        mavenCentral()
        maven {
            url 'http://oss.sonatype.org/content/repositories/snapshots'
        }
    }
    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    }
}


// Distribution

// More information about the following code can be found in the wiki.
// TODO: I bet this could be simplified so much...

task distJar(type: Jar) {
    archiveName = "kotlinfx-${rootProject.version}.jar"
    from project(':kotlinfx-core').sourceSets.main.output.classesDir
}

// It is assumed that the KotlinFX-pages dir that is a clone of the gh-pages branch
// is a sibling of the KotlinFX project dir.
def ghpagesPath = "${projectDir.getParentFile()}/KotlinFX-pages"
def localRepoPath = ghpagesPath+"/repository"

project(":kotlinfx-core") {
    apply plugin: 'maven'
    group = "kotlinfx"
    version = "0.1-SNAPSHOT"
    archivesBaseName = 'kotlinfx'
    uploadArchives {
        repositories.mavenDeployer {
            repository(url: "file:///" + localRepoPath)
            pom.version = '0.1-SNAPSHOT'
            pom.artifactId = 'kotlinfx'
            pom.groupId = 'kotlinfx'
        }
    }

// I just want to do it like this >:(!
//task publishgh {
//    workingDir ghpagesPath
//    commandLine './clear-repository.sh'
//    uploadArchives
//    commandLine './update-directory-index.sh'
//    commandLine 'git add . && git commit --amend -m "Upload binaries" && git push -f'
//}

    task clearRepo(type:Exec) {
        workingDir ghpagesPath
        commandLine './clear-repository.sh'
    }

    task updateIndex(type:Exec, dependsOn: [clearRepo, uploadArchives]) {
        workingDir ghpagesPath
        commandLine './update-directory-index.sh'
    }

    task publishgh0(type:Exec, dependsOn: updateIndex) {
        workingDir ghpagesPath
        commandLine 'git', 'add', '--all'
    }

    task publishgh1(type:Exec, dependsOn: publishgh0) {
        workingDir ghpagesPath
        commandLine 'git', 'commit', '--amend', '-m', '"Upload binaries"'
    }

    task publishgh(type:Exec, dependsOn: publishgh1) {
        workingDir ghpagesPath
        commandLine 'git', 'push', '-f'
    }
}


